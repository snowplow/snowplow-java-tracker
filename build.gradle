/*
 * Copyright (c) 2017 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'

wrapper.gradleVersion = '5.0.0'

group = 'com.snowplowanalytics'
archivesBaseName = 'snowplow-java-tracker'
version = '0.8.4'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def javaVersion = JavaVersion.VERSION_1_8

repositories {
    // Use 'maven central' for resolving our dependencies
    mavenCentral()
    // Use 'jcenter' for resolving testing dependencies
    jcenter()
}

configure([compileJava, compileTestJava]) {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = 'UTF-8'
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    // Apache Commons
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-net:commons-net:3.3'

    // Apache HTTP
    optional 'org.apache.httpcomponents:httpclient:4.3.3'
    optional 'org.apache.httpcomponents:httpasyncclient:4.0.1'

    // Square OK HTTP
    optional 'com.squareup.okhttp:okhttp:2.2.0'

    // SLF4J logging API
    compile 'org.slf4j:slf4j-api:1.7.7'
    testRuntime 'org.slf4j:slf4j-simple:1.7.7'

    // Jackson JSON processor
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1.1'

    // Preconditions
    compile 'com.google.guava:guava:18.0'

    // Testing libraries
    testCompile 'junit:junit:4.11'
    testCompile 'com.github.tomakehurst:wiremock:1.53'
    testCompile 'org.skyscreamer:jsonassert:1.2.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.squareup.okhttp:mockwebserver:2.1.0'
}

task sourceJar(type: Jar, dependsOn: 'generateSources') {
    from sourceSets.main.allJava
}

task generateSources {
    project.ext.set("outputDir", "$projectDir/src/main/java/com/snowplowanalytics/snowplow/tracker")
    doFirst {
        println outputDir
        def srcFile = new File((String) outputDir, "Version.java")
        srcFile.parentFile.mkdirs()
        srcFile.write(
                """/*
 * Copyright (c) 2014-2018 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */
package com.snowplowanalytics.snowplow.tracker;
// DO NOT EDIT. AUTO-GENERATED.
public class Version {
    static final String TRACKER = "java-$project.version";
    static final String VERSION = "$project.version";
}
""")
    }
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java


task printVersion {
    doLast {
        print "$project.version"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId 'snowplow-java-tracker'
            version version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'snowplow-java-tracker'
        pom.version = "$project.version"
        pom.project {
            name = 'snowplow-java-tracker'
            description = 'Snowplow event tracker for Java. Add analytics to your Java desktop and server apps, servlets and games.'
            url = 'https://github.com/snowplow/snowplow-java-tracker/'
            inceptionYear = '2014'

            packaging = 'jar'
            groupId = 'com.snowplowanalytics'

            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
            scm {
                connection = 'https://github.com/snowplow/snowplow-java-tracker.git'
                url = 'https://github.com/snowplow/snowplow-java-tracker'
            }
            developers {
                developer {
                    name = 'Snowplow Analytics Ltd'
                    email = 'support@snowplowanalytics.com'
                    organization = 'Snowplow Analytics Ltd'
                    organizationUrl = 'http://snowplowanalytics.com'
                }
            }
            organization {
                name = 'com.snowplowanalytics'
                url = 'http://snowplowanalytics.com'
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_SNOWPLOW_MAVEN_USER')
    key = System.getenv('BINTRAY_SNOWPLOW_MAVEN_API_KEY')

    publish = true

    pkg {
        repo = 'snowplow-maven'
        name = 'snowplow-java-tracker'

        group = 'com.snowplowanalytics'
        userOrg = 'snowplow'

        websiteUrl = 'https://github.com/snowplow/snowplow-java-tracker'
        vcsUrl = 'https://github.com/snowplow/snowplow-java-tracker'
        issueTrackerUrl = 'https://github.com/snowplow/snowplow-java-tracker/issues'

        licenses = ['Apache-2.0']
        publications = ['mavenJava']

        version {
            name = "$project.version"
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = System.getenv('SONA_USER')
                password = System.getenv('SONA_PASS')
                close = '1'
            }
        }
    }
}
