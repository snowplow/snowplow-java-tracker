/*
 * Copyright (c) 2017 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'java-library'
    id 'maven-publish'
    id 'idea'
}

wrapper.gradleVersion = '6.5.0'

group = 'com.snowplowanalytics'
archivesBaseName = 'snowplow-java-tracker'
version = '0.9.0'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def javaVersion = JavaVersion.VERSION_1_8

repositories {
    // Use 'maven central' for resolving our dependencies
    mavenCentral()
    // Use 'jcenter' for resolving testing dependencies
    jcenter()
}

configure([compileJava, compileTestJava]) {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = 'UTF-8'
}

java {
    registerFeature('okhttpSupport') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('apachehttpSupport') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    // Apache Commons
    api 'commons-codec:commons-codec:1.10'
    api 'commons-net:commons-net:3.3'

    // Apache HTTP
    apachehttpSupportApi 'org.apache.httpcomponents:httpclient:4.3.3'
    apachehttpSupportApi 'org.apache.httpcomponents:httpasyncclient:4.0.1'

    // Square OK HTTP
    okhttpSupportApi 'com.squareup.okhttp3:okhttp:4.2.2'

    // SLF4J logging API
    api 'org.slf4j:slf4j-api:1.7.7'
    testImplementation 'org.slf4j:slf4j-simple:1.7.7'

    // Jackson JSON processor
    api 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

    // Preconditions
    api 'com.google.guava:guava:29.0-jre'

    // Testing libraries
    testImplementation 'junit:junit:4.11'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'com.github.tomakehurst:wiremock:2.26.3'
    testImplementation 'org.skyscreamer:jsonassert:1.2.3'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.1'
}

task sourceJar(type: Jar, dependsOn: 'generateSources') {
    from sourceSets.main.allJava
}

task generateSources {
    project.ext.set("outputDir", "$projectDir/src/main/java/com/snowplowanalytics/snowplow/tracker")
    doFirst {
        println outputDir
        def srcFile = new File((String) outputDir, "Version.java")
        srcFile.parentFile.mkdirs()
        srcFile.write(
                """/*
 * Copyright (c) 2014-2019 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */
package com.snowplowanalytics.snowplow.tracker;
// DO NOT EDIT. AUTO-GENERATED.
public class Version {
    static final String TRACKER = "java-$project.version";
    static final String VERSION = "$project.version";
}
""")
    }
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java


task printVersion {
    doLast {
        print "$project.version"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId 'snowplow-java-tracker'
            version version
            suppressPomMetadataWarningsFor('apachehttpSupportApiElements')
            suppressPomMetadataWarningsFor('apachehttpSupportRuntimeElements')
            suppressPomMetadataWarningsFor('okhttpSupportApiElements')
            suppressPomMetadataWarningsFor('okhttpSupportRuntimeElements')
            pom {
                name = 'snowplow-java-tracker'
                description = 'Snowplow event tracker for Java. Add analytics to your Java desktop and server apps, servlets and games.'
                url = 'https://github.com/snowplow/snowplow-java-tracker/'
                inceptionYear = '2014'

                packaging = 'jar'
                groupId = 'com.snowplowanalytics'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    connection = 'https://github.com/snowplow/snowplow-java-tracker.git'
                    url = 'https://github.com/snowplow/snowplow-java-tracker'
                }
                developers {
                    developer {
                        name = 'Snowplow Analytics Ltd'
                        email = 'support@snowplowanalytics.com'
                        organization = 'Snowplow Analytics Ltd'
                        organizationUrl = 'http://snowplowanalytics.com'
                    }
                }
                organization {
                    name = 'com.snowplowanalytics'
                    url = 'http://snowplowanalytics.com'
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_SNOWPLOW_MAVEN_USER')
    key = System.getenv('BINTRAY_SNOWPLOW_MAVEN_API_KEY')

    publish = true

    pkg {
        repo = 'snowplow-maven'
        name = 'snowplow-java-tracker'

        group = 'com.snowplowanalytics'
        userOrg = 'snowplow'

        websiteUrl = 'https://github.com/snowplow/snowplow-java-tracker'
        vcsUrl = 'https://github.com/snowplow/snowplow-java-tracker'
        issueTrackerUrl = 'https://github.com/snowplow/snowplow-java-tracker/issues'

        licenses = ['Apache-2.0']
        publications = ['mavenJava']

        version {
            name = "$project.version"
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = System.getenv('SONA_USER')
                password = System.getenv('SONA_PASS')
                close = '1'
            }
        }
    }
}
