/*
 * Copyright (c) 2014 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

wrapper.gradleVersion = '2.6'

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: "com.jfrog.artifactory"


group = 'com.snowplowanalytics'
version = '0.8.2'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    // Use 'maven central' for resolving our dependencies
    mavenCentral()
    // Use 'jcenter' for resolving testing dependencies
    jcenter()
}

configure([compileJava, compileTestJava]) {
    options.encoding = 'UTF-8'
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'libs-release-local'
            maven = true
            username = "${artifactory_user}"
            password = "${artifactory_password}"

        }
        defaults {
            publishConfigs('archives', 'published')
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true

        }
    }

}

dependencies {

    // Apache Commons
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-net:commons-net:3.3'

    // Apache HTTP
    optional 'org.apache.httpcomponents:httpclient:4.3.3'
    optional 'org.apache.httpcomponents:httpasyncclient:4.0.1'
    
    // Square OK HTTP
    optional 'com.squareup.okhttp:okhttp:2.2.0'
    
    // SLF4J logging API
    compile 'org.slf4j:slf4j-simple:1.7.7'

    // Jackson JSON processor
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1.1'

    // Preconditions
    compile 'com.google.guava:guava:18.0'

    // Testing libraries
    testCompile 'junit:junit:4.11'
    testCompile 'com.github.tomakehurst:wiremock:1.53'
    testCompile 'org.skyscreamer:jsonassert:1.2.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.squareup.okhttp:mockwebserver:2.1.0'
}

task sourceJar(type: Jar, dependsOn: 'generateSources') {
    from sourceSets.main.allJava
}

// Publishing
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'snowplow-java-tracker'
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
//    repositories {
//        maven {
//            url "$buildDir/repo" // change to point to your repo, e.g. http://my.org/repo
//        }
//    }
}

task generateSources {
    project.ext.set("outputDir", "$projectDir/src/main/java/com/snowplowanalytics/snowplow/tracker")
    doFirst {
        println outputDir
        def srcFile = new File((String)outputDir, "Version.java")
        srcFile.parentFile.mkdirs()
        srcFile.write(
                """/*
 * Copyright (c) 2014-2015 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

package com.snowplowanalytics.snowplow.tracker;

// DO NOT EDIT. AUTO-GENERATED.
public class Version {
    static final String TRACKER = "java-$project.version";
    static final String VERSION = "$project.version";
}
""")
    }
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

artifactoryPublish.dependsOn publishToMavenLocal