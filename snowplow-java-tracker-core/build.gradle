/*
 * Copyright (c) 2014 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

// Applying the java plugin to add support for Java
apply plugin: 'java'
// As per http://www.gradle.org/docs/current/userguide/publishing_maven.html
apply plugin: 'maven-publish'

group = 'com.snowplowanalytics'
version = '0.2.0'

// Where to find the dependencies of our project
repositories {
    // Use 'maven central' for resolving our dependencies
    mavenCentral()
    // Use 'jcenter' for resolving testing dependencies
    jcenter()
}

// Dependencies for our production and test code
dependencies {

    // Apache Commons
    compile 'commons-codec:commons-codec:1.2'
    compile 'commons-net:commons-net:3.3'

    // Apache HTTP
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'org.apache.httpcomponents:httpasyncclient:4.0.1'

    // SLF4J logging API
    compile 'org.slf4j:slf4j-simple:1.7.7'

    // Jackson JSON processor
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1.1'

    // Testing libraries
    testCompile 'junit:junit:4.11'
    testCompile 'com.github.tomakehurst:wiremock:1.+'
}

task sourceJar(type: Jar, dependsOn: 'generateSources') {
    from sourceSets.main.allJava
}

// Publishing
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo" // change to point to your repo, e.g. http://my.org/repo
        }
    }
}

task generateSources {
    project.ext.set("outputDir", "$projectDir/src/main/java/com/snowplowanalytics/snowplow/tracker/core")
    doFirst {
        println outputDir
        def srcFile = new File((String)outputDir, "Version.java")
        srcFile.parentFile.mkdirs()
        srcFile.write(
"""/*
 * Copyright (c) 2014 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

package com.snowplowanalytics.snowplow.tracker.core;

// DO NOT EDIT. AUTO-GENERATED.
public class Version {
    static final String TRACKER = "java-core-$project.version";
    static final String VERSION = "$project.version";
}
""")
    }
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java
